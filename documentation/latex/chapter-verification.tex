\section{Verification of consistency}

A software project specification can be verified for consistency within Reprotool.
This section provides details about the verification process.

The system's behaviour is specified by the set of use-cases contained within the project.
Use-cases and their relations are stored in a memory structure we refer to as a use-case model (Figure~\ref{fig:ReprotoolUCModel}).
The model is based on the EMF framework so that it integrates well within the eclipse environment.

\begin{figure}[ht]
  \centering
  \includegraphics[width=260pt]{images/ReprotoolUCModel}
  \caption{Use-case model}
  \label{fig:ReprotoolUCModel}
\end{figure}

\subsection{Annotations model}

When we refer to verifying the consistency of a system, we actually consider the consistency of temporal properties implied by the annotations used within the project.

Every reprotool project contains a set of temporal constraints imposed upon annotations in the project.
Annotations\footnote{An intuitive example of annotations is the annotation pair \emph{open-close}. For such annotations we could describe constraints like \emph{After open there should always be close}, or \emph{No multi open without close}.} can be attached to individual use-case steps (see \emph{StepAnnotation} element depicted in Figure~\ref{fig:ModelOfAnnotations}).


An annotation attached to a use-case step consists of two parts\footnote{An example of an annotation type we use in Reprotool is \emph{open}.
A suitable \emph{id} for annotation of this type could be \emph{file1}, thus the description of this annotation reads \textcolor{Blue}{\emph{open\_file1}}.}
-- \emph{type} and \emph{id}.

\begin{figure}[ht]
  \centering
  \includegraphics[width=280pt]{images/ReprotoolUCAnnotModel}
  \caption{Model of annotations}
  \label{fig:ModelOfAnnotations}
\end{figure}

Every reprotool project specifies a "vocabulary" of annotations (i.e. types of annotations) that users can use to annotate use-case steps. When assigning a \emph{StepAnnotation} to a given step, users would combine \emph{StepAnnotationType} with an arbitrary \emph{id}.

Annotations are organized in groups as depicted in Figure~\ref{fig:ModelOfAnnotations}. Currently, Reprotool supports two kinds of annotations:
\begin{description}

	\item[Temporal annotations] organized in \emph{TemporalAnnotationGroup}s, whose semantics is specified by a set of temporal logic formulae; their format will be described later on.
	
	\item[Special annotations] organized in \emph{SpecialAnnotationGroup}s, whose semantics is hard-coded in Reprotool.
\end{description}

\subsection{Special annotations}

When a \emph{reprotool} user creates a new project from the \emph{eclipse IDE} new project wizard, a special \emph{trace-on} annotation group
is added to the project. This annotation group contains two special annotations - \emph{trace} and \emph{on}. These are the only special
annotations supported by the \emph{reprotool} system. These two special annotations enable the \emph{reprotool} users to select mutually exclusive paths in their use-case scenarios. This might be used to prune the
computation tree the \emph{NuSMV} tool considers when checking the \emph{CTL}/\emph{LTL} specifications.

Let's assume we have a use-case \emph{U} that has the main scenario 7 steps long. Step 1 and the step 4 have both an extension scenario 3
steps long. The individual use-case steps are annotated as depicted in the following picture:


\begin{figure}[ht]
  \centering
  \includegraphics[width=180pt]{images/traceTest_no_trace}
  \caption{Schema of the use-case \emph{U}}
  \label{fig:traceTest_no_trace}
\end{figure}

The use-case \emph{U} contains \emph{create} and \emph{use} annotations. An example of a constraint that is by default imposed on such
annotations in new \emph{reprotool} projects is \emph{Only one create}. That means you can not create a given object more than once. But if
you look at the use-case \emph{U} carefully, you may notice that it is quite easy to find an execution path that creates \emph{FILE\_A}
two times. You simply take both extension scenarios in your execution path.

To solve the problem in the previous example, user needs to specify that if the first extension scenario has been taken, then the
second extension scenario of use-case step 4 can not be taken. On the other hand, if the first extension scenario has not been taken,
then the second extension scenario must be taken. And this is exactly what the \emph{trace} and \emph{on} annotations are
for.

This is how these annotations work: Whenever a use-case step has \emph{on} annotation specified with id \emph{A}, the paths
involving this step will be considered only if the \emph{trace} annotation with the same id \emph{A} has previously been encountered. Let's
clarify it with the help of an example. In the following figure you can see the same use-case \emph{U} but with properly added
\emph{trace} and \emph{on} annotations that prevent any conflicts.

\begin{figure}[ht]
  \centering
  \includegraphics[width=180pt]{images/traceTest}
  \caption{Schema of the use-case \emph{U} with trace/on annotations added}
  \label{fig:traceTest}
\end{figure}

The annotated use-case \emph{U} has now only two execution paths possible. And none of them violates any of the default constraints
imposed on the \emph{create} and \emph{use} annotations. Here you can see the path where the \emph{trace}/\emph{on} branch has been
taken:

\begin{figure}[ht]
  \centering
  \includegraphics[width=180pt]{images/traceTest_path_taken}
  \caption{\emph{Trace}/\emph{on} branch taken}
  \label{fig:traceTestTaken}
\end{figure}

When the \emph{NuSMV} tool simlates the execution of the state machine corresponding to the annotated use-case \emph{U} and just finished
execution of the 4 step of the use-case main scenario, there would normally by 2 possibilities how to proceed. But because in our
example execution path the \emph{trace\_a} annotaion has been encountered, there is only one possibility - and that is to take the
\emph{on} branch.

\newpage

The situation where the second path has been taken is depicted in the next picture. No other execution paths are considered by the
\emph{NuSMV} tool.

\begin{figure}[ht]
  \centering
  \includegraphics[width=180pt]{images/traceTest_path_not_taken}
  \caption{\emph{Trace}/\emph{on} branch NOT taken}
  \label{fig:traceTestNotTaken}
\end{figure}

\subsection{NuSMV symbolic model checker}

We use NuSMV~\cite{NuSMV-CAV02,NuSMV-frocos02} symbolic model-checker to verify consistency of use-cases with regard to their temporal annotations.
Although NuSMV support BDD-based\footnote{Using binary decision diagrams} and BMC-based\footnote{Bounded model-checking using a SAT solver} model-checking techniques, in our project we use just the BDD-based approach.
NuSMV supports analysis of synchronous and asynchronous systems using Computation Tree Logic (CTL) and Linear Temporal Logic (LTL).
Our framework supports both CTL and LTL, however CTL is preferred because NuSMV would converts LTL formulae into CTL internally (as described in \cite{NuSMV-ltl-fmsd97}).

All use-cases from our UC model have to be converted into NuSMV input language.
We use Xtext\cite{Xtext-website} as a tool for handling DSLs (Domain Specific Languages) which in this case is the NuSMV input language.

NuSMV input language is described using EBNF grammar (inspired by \cite{googlecode-nusmv-tools}) and Xtext generates:
\begin{itemize}
  \item an in-memory representation of the parse tree, sometimes being referred to as an Abstract Syntax Tree (AST),
  \item a text-to-AST parser and
  \item a AST-to-text serializer
\end{itemize}

In order to convert our UC model into NuSMV input language, we use the model-to-model (M2M) approach.
We traverse the UC model and during that traversal we collect the neccesary information to build a new EMF memory structure based on the generaetd NuSMV model.
The generated serializer then produces a valid NuSMV code from the NuSMV model.

\begin{figure}[ht]
  \centering
  \includegraphics[height=280pt]{images/XtextWorkflow}
  \caption{Transformation of a SW Project into the input language of a model checker}
  \label{fig:XtextWorkflow}
\end{figure}
\pagebreak

\subsection{Generated NuSMV model}

The NuSMV language model is based on the EMF framework and is generated from the NuSMV language grammar.
This model describes the classes that build up the AST (Abstract Syntax Tree) of the NuSMV language.

Here we present an excerpt from the NuSMV language grammar. You can find the whole grammar in th
\emph{reprotool.fm.nusmv.lang} plugin in the \emph{NuSMVLang.xtext} file.

\begin{lstlisting}[language=XtextGrammar]
grammar reprotool.fm.nusmv.lang.NuSmvLang with org.eclipse.xtext.common.Terminals
generate nuSmvLang "http://d3s.mff.cuni.cz/reprotool/nusmv/lang"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Module:
	"MODULE" (MainModule | OtherModule)
	(moduleElement+=ModuleElement)*;
MainModule:
	name='main';

OtherModule:
	name=ID ("(" (params+=FormalParameter) ("," params+=FormalParameter)* ")")?;

ModuleElement hidden(WS, SL_COMMENT):
	  VariableDeclaration
	| IVariableDeclaration
	| FrozenVariableDeclaration
	| DefineDeclaration
	| ConstantsDeclaration
	| AssignConstraint
	| TransConstraint
	| InitConstraint
	| InvarConstraint
	| FairnessConstraint
	| CtlSpecification
	| LtlSpecification
	| InvarSpecification
;
\end{lstlisting}

The Xtext generates an EMF model from the specified grammar. You can have a hierarchical view of the generated model by displaying the
\emph{NuSMVLang.ecore} file in the \emph{reprotool.fm.nusmv.lang} plugin. To better visualise the model, right-click on the \emph{NuSMVLang.ecore} file in
the package explorer of the eclipse IDE and click the option ``Initialize Ecore Diagram file...''. This will create the \emph{NuSMVLang.ecorediag} file that you can view with the provided eclipse editor.

\begin{figure}[ht]
  \centering
  \includegraphics[width=260pt]{images/ReprotoolActionsModel}
  \caption{Model of derived action}
  \label{fig:ReprotoolActionsModel}
\end{figure}

\subsection{Model to model transformation}

Here we present an overview of the transformation of our UC model into the generated NuSMV model. The objective of this process is to take a reprotool project as input and transform it into the generated NuSMV model.

All transformations are implemented using the Xtend~\cite{Xtend-website} language.
Xtend is a statically-typed programming language which is compiled to Java and therefore integrates well and runs on JVM. It brings many concepts simplifying model-to-model and model-to-text transformation. We have leveraged on the following Xtend features: 
\begin{itemize}
	\item Advanced type inference
	\item Extension methods - enhance closed types with new functionality.
	\item Closures - concise syntax for anonymous function literals.
		These features are used for implementing the builder design pattern.
	
		\verb|$(...factory code...)[...initialization code...]|
	
	\item Multiple dispatch a.k.a. polymorphic method invocation.
	\item Operator overloading (e.g. \verb|map += key -> value|)
	\item Property access syntax - shorthands for getter and setter access.
\end{itemize}

This transformation is implemented in the \emph{reprotool.fm.nusmv} plugin in the \emph{reprotool.fm.nusmv.mapping} package.
The transformation itself is quite straight-forward and is defined in an Xtend class \emph{NuSMVProj.xtend}.
For every use-case we create a definition of a non-deterministic state machine.
The state machine's states are directly related to the steps of the related use-case.
The use-cases in the reprotool project can have precedence relations specified. Theses relations are considered in the
transformation process. The use-case state machines are instantiated with a parameter that triggers the execution of the machine.
With this parameter we ensure that the precedence relations are fulfilled. By means of this parameter we also avoid parallel execution
of multiple use-cases.

One part of this model transformation process is to generate NuSMV CTL/LTL formulas which are to be checked by the NuSMV tool. The
skeletons of these formulas are provided by the user in the reprotool project. In this model to model transformation process these
formula skeletons are simply expanded by substituting the annotation patterns with annotations found in the steps of the use-cases
in the reprotool project. A detailed example with explanation is provided in the next section.

\subsection{Example of a reprotool project converted into NuSMV}

In this section we present a simple reprotool project consisting of two use-cases. We show by the means of an example how this project
gets transformed into the NuSMV language format.

The project contains two use-cases. For every use-case U one state machine M is generated. This machine is represented by the states and
transitions between them that are derived directly from the use-case steps of the use-case U. Let's assume our project contains a use
case U1. The use-case U1 has a main scenario that contains five use-case steps. Use-case step 1 is annotated by the annotation \emph{open\_file}
and use-case step 2 is annotated by the annotation \emph{close\_file}. Step 2 has two extension scenarios (One is three steps and the other
one two steps long) and step 4 has one extension scenario two steps long. Step 4 includes another use-case U2 from the project. The
main scenario of use-case U2 contains only single step. You can have a look at the visual representation of the use-case U1 (and also
use-case U2 since it is included in the step 4 of the main scenario of the use-case U1) in the following figure. The initial state is
filled with black colour and the final state is filled with green colour. Steps 1 and 2 are marked with a violet square because these
steps are annotated.

\newpage

\begin{figure}[h]
  \centering
  \includegraphics[width=200pt]{images/u1}
  \caption{Visual representation of the use-case U1}
  \label{fig:use-case U1}
\end{figure}

Now we present the state machine M that represents the use-case U1 and is written down using the NuSMV syntax. The state machine is
represented by a single module UC\_U1. This module contains a definition of variable s to which values representing variour machine
states can be assigned. Indeed, the transition to state s1 in the state machine is simulated by assigning the value s1 to the
variable s. The biggest part of the module UC\_U1 specification takes up a case construct that specifies various state transitions
of the machine based on the current state. Becuase use-case U1 includes another use-case U2 (which is represented by the module
UC\_U2), the module UC\_U1 also contains a variable y1 that is an instantiation of the module UC\_U2 included in the module UC\_U1.
\begin{lstlisting}
MODULE UC_U1 ( top , run )
	VAR y1run : boolean ;
	INIT y1run in FALSE
	VAR y1 : UC_U2 ( top , y1run ) ;
	ASSIGN next ( y1run ) := (s=s3__) ;
	VAR s : { s0 , s_ext_3 , s_ext_2 , s2 , s1 , s3__ , s2a2 , s3_ , s2_ , s2b1 ,
		s3a1 , s1_ , s2a1 , s3 , sFin } ;
	INIT s in s0
	ASSIGN next ( s ) := case
		s=s0 & !run : s0;
		s=s0 & run : {s1_};
		s=s3__ & y1.s != sFin : s3__;
		s=s3__ & y1.s  = sFin : s3_;
		s=s_ext_2 : {s3__};
		s=s2 : {s2a1,s2b1,s_ext_2};
		s=s1 : {s2_};
		s=s2a2 : {s_ext_2};
		s=s3_ : s3;
		s=s2_ : s2;
		s=s2b1 : {s_ext_2};
		s=s3a1 : {s_ext_3};
		s=s1_ : s1;
		s=s2a1 : {s2a2};
		s=s3 : {s3a1,s_ext_3};
		s=s_ext_3 : sFin;
		s=sFin : sFin;
	esac ;
\end{lstlisting}

Here we present the machine M2 that represents the included use-case U2.
\begin{lstlisting}
MODULE UC_U2 ( top , run )
	VAR s : { s0 , s1 , sFin } ;
	INIT s in s0
	ASSIGN next ( s ) := case
		s=s0 & !run : s0;
		s=s0 & run : {s1};
		s=s1 : sFin;
		s=sFin : sFin;
	esac ;
\end{lstlisting}

As we have already mentioned when describing use-case U1, the first two steps of its main scenario are annotated. (This is denoted
by a small violet square in the graphical representation of the use-case U1). Every such an annotation generates an annotation variable
definition in the NuSMV model. The variable name encodes the annotation type (for example open) and the annotation id (for example
file1). The variable is of boolean type and is initialized with the logical value of zero. The variable value is set to logical one
if and only if the state variable s of the respective state machine module denotes that the machine is in the annotated state.

Let's clarify this a bit more with the example of our project. There we have use-case step one of the use-case U1 annotated with the
open annotation (annotation id is file1) and step two annotated with the close annotation (annotation id is file1). These two use-case
step annotations in the use-case U1 of our project yield the following annotation variable definitions:

\begin{lstlisting}
VAR open_file1 : boolean ;
INIT open_file1 in FALSE
ASSIGN next ( open_file1 ) := FALSE
	| xU1.s in {s1_} ;
VAR close_file1 : boolean ;
INIT close_file1 in FALSE
ASSIGN next ( close_file1 ) := FALSE
	| xU1.s in {s2_} ;
\end{lstlisting}

Now we will describe the variable named \emph{p} which has the function of a steering wheel of whole simulation performed by the \emph{NuSMV} tool
when looking for possible violations of our system. This variable is of type enum and can assume the value \emph{none}, or other n values that
map directly to the n use-cases present in our project. The \emph{p} variable is initialised with the \emph{none} value. When the \emph{NuSMV} tool performs
steps in our system which are basicly transitions in the state machines of our use-cases, the value of the \emph{p} variable alternates between
the \emph{none} value and any other value from the set of possible values. Thus, the \emph{p} variable definition looks like this:
\begin{lstlisting}
VAR p : { none , pU1 , pU2} ;
INIT p in none
ASSIGN next ( p ) := case
	p=none : {pU1, pU2};
	TRUE : none;
esac ;
\end{lstlisting}

The \emph{p} variable helps us to decide which state machine could be executed in the next steps. We also generate definition for the
boolean variable \emph{idle} that tells us if any of the use-case state machines is currently being executed. The \emph{idle} variable is defined
by the means of a set of boolean variables from which every variable tells us if the machine to which it maps is currently
being executed. This is how it looks in the \emph{NuSMV} syntax:
\begin{lstlisting}
VAR idle : boolean ;
INIT idle in TRUE
ASSIGN next ( idle ) := case
	xU1run | xU2run : FALSE;
	TRUE : TRUE;
esac ;
\end{lstlisting}

Next we need to define for every use-case \emph{UN} a boolean variable \emph{xUNrun} that triggers the execution of the respective state machine
and at the same time serves as an indicator if the respective state machine N is currently running. This boolean variable is
initialised with logical zero. It is assigned the logical one only if all other machines are idle and the p variable points to the
machine N. For every use-case UN we also need to define a variable \emph{xUN} that is the actual instantiation of the state machine N.
Of course this variable is of type \emph{UC\_UN}. This is how the definitions of the variables \emph{xUN} and \emph{xUNrun} look in practice:
\begin{lstlisting}
VAR xU1 : UC_U1 ( self , xU1run ) ;
VAR xU1run : boolean ;
INIT xU1run in FALSE
ASSIGN next ( xU1run ) := case
	p=pU1 & idle & xU1.s = s0 : TRUE;
	TRUE : xU1run & xU1.s != sFin;
esac ;
\end{lstlisting}

Now we are going to describe how the \emph{CTL} and \emph{LTL} specifications are generated. When a new empty reprotool project is generated in the
\emph{eclipse IDE}, it already contains a set of predefined \emph{CTL} formulas along with a quick description of each formula. Here is a subset of
these predefined formulas that is relevant to the \emph{open}/\emph{close} annotations:

\begin{description}
 \item [$AG(open \rightarrow AF(close))$] After 'open' there should always be 'close'
 \item [$AG(open \rightarrow AX(A\lbrack!open \cup close\rbrack))$] No multi-open without close
 \item [$AG(close \rightarrow AX(A\lbrack!close \cup open \mid !AF(close) \rbrack))$] No multi-close without open
 \item [$A\lbrack !close \cup open \mid !AF(close)\rbrack$] First 'open' then 'close'
\end{description}

We can see that these logical formulas specify properties for the open and close annotations. We also know that in the use-case \emph{U1} we
use annotation \emph{open\_file1} and \emph{close\_file1}. Now we simply expand the predefined \emph{CTL} and \emph{LTL} formulas. This is what we get in our
example:
\begin{lstlisting}
CTLSPEC AG(open_file1 -> AF(close_file1))
CTLSPEC AG(open_file1 -> AX(A[!open_file1 U close_file1]))
CTLSPEC AG(close_file1 -> AX(A[!close_file1 U open_file1 | !AF(close_file1) ]))
CTLSPEC A[!close_file1 U open_file1 | !AF(close_file1)]
\end{lstlisting}

And this is what the \emph{NuSMV} file looks like:
\begin{lstlisting}
MODULE main
	CTLSPEC AG(open_file1 -> AF(close_file1))
	CTLSPEC AG(open_file1 -> AX(A[!open_file1 U close_file1]))
	CTLSPEC AG(close_file1 -> AX(A[!close_file1 U open_file1 | !AF(close_file1) ]))
	CTLSPEC A[!close_file1 U open_file1 | !AF(close_file1)]

	FAIRNESS p=pU1
	FAIRNESS p=pU2
	FAIRNESS p=pU3

	VAR p : { none , pU1 , pU2} ;
	INIT p in none
	ASSIGN next ( p ) := case
		p=none : {pU1, pU2};
		TRUE : none;
	esac ;

 	VAR idle : boolean ;
	INIT idle in TRUE
	ASSIGN next ( idle ) := case
		xU1run | xU2run : FALSE;
		TRUE : TRUE;
	esac ;

 	VAR xU1 : UC_U1 ( self , xU1run ) ;
	VAR xU1run : boolean ;
	INIT xU1run in FALSE
	ASSIGN next ( xU1run ) := case
		p=pU1 & idle & xU1.s = s0 : TRUE;
		TRUE : xU1run & xU1.s != sFin;
	esac ;

	VAR xU2 : UC_U2 ( self , xU2run ) ;
	VAR xU2run : boolean ;
	INIT xU2run in FALSE
	ASSIGN next ( xU2run ) := case
		p=pU2 & idle & xU2.s = s0 : TRUE;
		TRUE : xU2run & xU2.s != sFin;
	esac ;

 	VAR open_file1 : boolean ;
	INIT open_file1 in FALSE
	ASSIGN next ( open_file1 ) := FALSE
		| xU1.s in {s1_}
	;

	VAR close_file1 : boolean ;
	INIT close_file1 in FALSE
	ASSIGN next ( close_file1 ) := FALSE
		| xU1.s in {s2_}
	;

MODULE UC_U1 ( top , run )
	VAR y1run : boolean ;
	INIT y1run in FALSE
	VAR y1 : UC_U2 ( top , y1run ) ;
	ASSIGN next ( y1run ) := (s=s3__) ;
	VAR s : { s0 , s_ext_3 , s_ext_2 , s2 , s1 , s3__ , s2a2 , s3_ , s2_ , s2b1 ,
			s3a1 , s1_ , s2a1 , s3 , sFin } ;
	INIT s in s0
	ASSIGN next ( s ) := case
		s=s0 & !run : s0;
		s=s0 & run : {s1_};
		s=s3__ & y1.s != sFin : s3__;
		s=s3__ & y1.s  = sFin : s3_;
		s=s_ext_2 : {s3__};
		s=s2 : {s2a1,s2b1,s_ext_2};
		s=s1 : {s2_};
		s=s2a2 : {s_ext_2};
		s=s3_ : s3;
		s=s2_ : s2;
		s=s2b1 : {s_ext_2};
		s=s3a1 : {s_ext_3};
		s=s1_ : s1;
		s=s2a1 : {s2a2};
		s=s3 : {s3a1,s_ext_3};
		s=s_ext_3 : sFin;
		s=sFin : sFin;
	esac ;

MODULE UC_U2 ( top , run )
	VAR s : { s0 , s1 , sFin } ;
	INIT s in s0
	ASSIGN next ( s ) := case
		s=s0 & !run : s0;
		s=s0 & run : {s1};
		s=s1 : sFin;
		s=sFin : sFin;
	esac ;
\end{lstlisting}
